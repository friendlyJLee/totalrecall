/*
 * Copyright (Â©) 2016 Jeff Harris <jefftharris@gmail.com>
 * All rights reserved. Use of the code is allowed under the
 * Artistic License 2.0 terms, as specified in the LICENSE file
 * distributed with this code, or available from
 * http://www.opensource.org/licenses/artistic-license-2.0.php
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Task to push to common repositories
tasks.addRule("Pattern: hgpush_<ID>") { String taskName ->
    if (taskName.startsWith("hgpush_")) {
        task(taskName, type:Exec) {
            executable = "hg"
            args = ["push", taskName - "hgpush_"]
            ignoreExitValue = true
            doLast {
                def rc = execResult.getExitValue()
                if ((rc != 0) && (rc != 1)) {
                    execResult.assertNormalExitValue()
                }
            }
        }
    }
}

task hgpush_all {
    description = "Mercurial push to common repositories"
    group = "other"

    dependsOn << "hg paths".execute().text.split("\n").collect({ line ->
        def path = line.split(" ")[0]
        "hgpush_$path"
    })
}
